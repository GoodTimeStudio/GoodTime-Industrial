buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sponge"
            url = "http://repo.spongepowered.org/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
//apply plugin: 'checkstyle'

version = "dev.0.3" + getBuildNumber()
group= "com.mcgoodtime.productionline"
archivesBaseName = "ProductionLine"

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to/"
    }
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven"
    }
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven"
    }
}

configurations {
    shade
    provided
    compile.extendsFrom configurations.shade
}

dependencies {
    shade 'org.spongepowered:mixin:0.5.14-SNAPSHOT'
    compile 'net.industrial-craft:industrialcraft-2:2.2.800-experimental:dev' //adds ic2 to the dev env
    //provided 'codechicken:NotEnoughItems:1.7.10-1.0.4.79:dev'
    //provided 'codechicken:CodeChickenCore:1.7.10-1.0.4.29:dev'
    //provided 'codechicken:CodeChickenLib:1.7.10-1.1.1.99:dev'
    //checkstyle 'com.puppycrawl.tools:checkstyle:6.7' //GRADLE-3314 gradle bug, not resolved
}

jar {
	configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))
	}
	
    manifest {
		attributes (
            'FMLCorePlugin': 'com.mcgoodtime.productionline.common.core.coremod.PLCore',
			'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'TweakOrder': 0,
			'MixinConfigs': 'mixins.productionline.core.json',
			'FMLCorePluginContainsFMLMod': true,
			'FMLAT': 'productionline_at.cfg',
			'ForceLoadAsMod': true
		)
    }
}

minecraft {
    version = "1.7.10-10.13.4.1558-1.7.10"
    runDir = "eclipse"
}


compileJava {

    options.encoding = 'UTF-8'
    options.compilerArgs += [
            '-Xlint:-processing',
            "-AreobfSrgFile=${project.file('build/srgs/mcp-srg.srg').canonicalPath}"
    ]
}

task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from project.plugins.getPlugin('forge').delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}
compileJava.dependsOn copySrgs

sourceSets {
    main {
        compileClasspath += configurations.provided
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

idea { 
    module {
        scopes.PROVIDED.plus += [configurations.provided]
        inheritOutputDirs = true 
    } 
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version' : project.version, 'mcversion' : project.minecraft.version
    }
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

def getBuildNumber() {
	return "$System.env.BUILD_NUMBER" != "null" ? "." + Integer.parseInt("$System.env.BUILD_NUMBER") : ""
}

tasks.jar.dependsOn "check"
