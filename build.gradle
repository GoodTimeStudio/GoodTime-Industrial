buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'checkstyle'

version = "dev.0.4" + getBuildNumber()
group= "com.mcgoodtime.productionline"
archivesBaseName = "ProductionLine"

minecraft {
    version = "1.10.2-12.18.1.2094"
    runDir = "run"
    mappings = "snapshot_20160518"
}

repositories {
    maven {
        name = "ic2"
        url = "http://maven.ic2.player.to"
    }
    maven {
        name = "sponge"
        url = "http://repo.spongepowered.org/maven"
    }
	maven {
		name = "jei"
		url = "http://dvs1.progwml6.com/files/maven"
	}
}

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    compile 'org.spongepowered:mixin:0.4.4'
    compile 'net.industrial-craft:industrialcraft-2:2.6.46-ex110:dev' //adds ic2 to the dev env
	compile 'mezz.jei:jei_1.10.2:3.9.0.246'
    checkstyle 'com.puppycrawl.tools:checkstyle:6.7' //GRADLE-3314 gradle bug, not resolved
}

jar {
	/*configurations.shade.copyRecursive().setTransitive(false).each { artifact ->
		from (zipTree(artifact))
	}*/

    configurations.shade.each { dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }


    manifest {
		attributes (
            'FMLCorePlugin': 'com.mcgoodtime.productionline.common.core.coremod.PLCore',
			'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
			'TweakOrder': 0,
			'MixinConfigs': 'mixins.productionline.core.json',
			'FMLCorePluginContainsFMLMod': true,
			'FMLAT': 'productionline_at.cfg',
			'ForceLoadAsMod': true
		)
    }
}

checkstyle { 
    toolVersion = "6.7" 
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += [
            '-Xlint:-processing',
            "-AreobfSrgFile=${project.file('build/srgs/mcp-srg.srg').canonicalPath}"
    ]
}

task copySrgs(type: Copy, dependsOn: 'genSrgs') {
    from project.plugins.getPlugin('net.minecraftforge.gradle.forge').delayedFile('{SRG_DIR}')

    include '**/*.srg'
    into 'build/srgs'
}
compileJava.dependsOn copySrgs

idea { 
    module {
        inheritOutputDirs = true 
    } 
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
	
    from (sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version' : project.version, 'mcversion' : project.minecraft.version
    }
	
    from (sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

checkstyle {
    configFile = file('checkstyle.xml')
    configProperties = [
            "name" : 'Production-Line',
            "organization" : 'GoodTimeStudio',
            "url" : 'https://github.com/GoodTimeStudio/Production-Line',
            "year" : '2016',
            "basedir": project.projectDir,
            "severity": 'warning'
    ]
}

def getBuildNumber() {
	return "$System.env.BUILD_NUMBER" != "null" ? "." + Integer.parseInt("$System.env.BUILD_NUMBER") : ""
}

tasks.jar.dependsOn "check"
